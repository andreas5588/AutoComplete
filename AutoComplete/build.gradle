import static org.gradle.api.JavaVersion.*

['distribution', 'maven-publish', 'signing'].each { apply plugin: it }

assert current().isJava8Compatible()

archivesBaseName = 'autocomplete'

dependencies {
    implementation files("${projectDir.absolutePath}/../../RSyntaxTextArea/RSyntaxTextArea/build/classes/java/main")
    implementation files("${projectDir.absolutePath}/../../RSyntaxTextArea/RSyntaxTextArea/build/resources/main")
}

ext.isReleaseVersion = !project.version.endsWith('SNAPSHOT')

ext.sharedManifest = manifest {
	attributes('Class-Path': 'rsyntaxtextarea.jar',
		'Specification-Title': 'AutoComplete',
		'Specification-Version': version,
		'Implementation-Title': 'org.fife.ui',
		'Implementation-Version': version)

}
jar {
	manifest { from sharedManifest }
}

// We use "distributions" to create the zip files uploaded to SourceForge
distributions {
	main {
        distributionBaseName = 'autocomplete'
		contents {
			from { [ 'build/libs' ] }
			rename 'AutoComplete-.*\\.jar', 'autocomplete.jar'
		}
	}
	src {
        distributionBaseName = 'autocomplete'
	}
}
distZip.classifier = null
distZip.dependsOn jar
srcDistZip.classifier = 'src'
srcDistZip {
	from projectDir
	include 'src/**/*'
	include 'build.gradle'
	include '.classpath'
	include '.project'
	include 'gradle.properties'
	include 'gradle/**/*'
	include 'gradlew*'
	include 'README.md'
	include '.settings/**'
}

task buildSourceForgeZips {
    doLast {
        println 'Building zip files for SourceForge'
    }
}
buildSourceForgeZips.dependsOn clean, jar, distZip, srcDistZip

publishing {
	repositories {
        maven {
            def releasesRepoUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
            def snapshotsRepoUrl = 'https://oss.sonatype.org/content/repositories/snapshots/'
            url = isReleaseVersion ? releasesRepoUrl : snapshotsRepoUrl
            credentials { // Credentials usually kept in user's .gradle/gradle.properties
                username = ossrhUsername
                password = ossrhPassword
            }
        }
    }
    publications {
        maven(MavenPublication) {

            groupId = 'com.fifesoft'
            artifactId = 'autocomplete'
            version = version

            from components.java

            pom {
                name = 'autocomplete'
                description = 'AutoComplete is a library allowing you to add IDE-like auto-completion ' +
                        '(aka "code completion" or "Intellisense") to any Swing JTextComponent. Special integration ' +
                        'is added for RSyntaxTextArea, since this feature is commonly needed when editing source code. ' +
                        'Features include: Drop-down completion choice list. Optional companion "description" window, ' +
                        'complete with full HTML support and navigable with hyperlinks. Optional parameter completion ' +
                        'assistance for functions and methods, ala Eclipse and NetBeans. Completion information is ' +
                        'typically specified in an XML file, but can even be dynamic.'
                url = 'http://www.fifesoft.com/rsyntaxtextarea/'
                inceptionYear = '2003'
                packaging = 'jar'
                licenses {
                    license {
                        name = 'BSD-3-Clause'
                        url = 'https://github.com/bobbylight/AutoComplete/blob/master/AutoComplete/src/main/dist/AutoComplete.License.txt'
                    }
                }
                developers {
                    developer {
                        name = 'Robert Futrell'
                    }
                }
                scm {
                    url = 'https://github.com/bobbylight/AutoComplete'
                    connection = 'scm:git:git://github.com/bobbylight/AutoComplete'
                    developerConnection = 'scm:git:git@github.com:bobbylight/AutoComplete'
                    if (isReleaseVersion) {
                        tag = project.version
                    }
                }
            }
        }
    }
}

signing {
    // Don't require signing for e.g. ./gradlew install
    required { gradle.taskGraph.hasTask('publish') && isReleaseVersion }
    sign publishing.publications.maven
}
tasks.withType(Sign) {
    onlyIf { isReleaseVersion }
}
