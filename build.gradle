buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'biz.aQute.bnd:biz.aQute.bnd.gradle:5.0.0'
    }
}

plugins {
    id 'com.github.spotbugs' version '4.0.1'
}

group = 'com.fifesoft'

allprojects {

    repositories {
        mavenCentral()
        maven {
            url 'https://oss.sonatype.org/content/repositories/snapshots'
        }
    }

    wrapper {
        gradleVersion = '6.2.2'
    }
}

subprojects {

    apply plugin: 'java'
    apply plugin: 'checkstyle'
    apply plugin: 'com.github.spotbugs'

    checkstyle {
        toolVersion = '8.30'
        configFile = '../config/checkstyle/checkstyle.xml' as File // Relative to subprojects
    }

    spotbugsMain {
        reports {
            html {
                enabled = true
            }
            xml {
                enabled = false
            }
        }
    }
    spotbugsTest {
        reports {
            html {
                enabled = true
            }
            xml {
                enabled = false
            }
        }
    }

    dependencies {
        testImplementation 'junit:junit:4.13'
    }

    compileJava {
        sourceCompatibility javaVersion
        targetCompatibility javaVersion
        options.debug = true
        options.debugOptions.debugLevel = 'source,vars,lines'
        options.compilerArgs << "-Xlint:deprecation" << '-Xlint:unchecked'
    }
}
