group = 'com.fifesoft'

allprojects {

    repositories {
        mavenCentral()
        maven {
            url "https://oss.sonatype.org/content/repositories/snapshots"
        }
    }

    wrapper {
        gradleVersion = '4.10'
    }
}

subprojects {

    apply plugin: 'java'
    apply plugin: 'checkstyle'
    apply plugin: 'findbugs'

    checkstyle {
        toolVersion = '8.11'
        configFile = '../config/checkstyle/checkstyle.xml' as File // Relative to subprojects
    }

    tasks.withType(FindBugs) {
        reports {
            xml.enabled = false
            html.enabled = true
        }
    }

    dependencies {
        testCompile 'junit:junit:4.12'
    }

    compileJava {
        sourceCompatibility javaVersion
        targetCompatibility javaVersion
        options.debug = true
        options.debugOptions.debugLevel = 'source,vars,lines'
        // Most folks will compile with the latest JDK available, but official builds use a
        // Java 6 boot classpath.  Add this property to gradle.properties for boot classpath
        if (project.hasProperty('java6CompileBootClasspath')) {
            if (new File(java6CompileBootClasspath).isFile()) {
                println "Bootstrap classpath when compiling Java: ${java6CompileBootClasspath}"
                options.bootstrapClasspath = files(java6CompileBootClasspath)
            }
            else {
                println "Warning: Specified java6CompileBootClasspath does not exist: ${java6CompileBootClasspath}"
            }
        }
        options.compilerArgs << "-Xlint:deprecation" << '-Xlint:unchecked'
    }
}
